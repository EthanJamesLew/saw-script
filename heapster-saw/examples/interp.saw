enable_experimental;
env <- heapster_init_env "interp" "interp.bc";

/***
 *** Types
 ***/

// Integer types
heapster_define_perm env "int64" " " "llvmptr 64" "exists x:bv 64.eq(llvmword(x))";
heapster_define_perm env "int32" " " "llvmptr 32" "exists x:bv 32.eq(llvmword(x))";
heapster_define_perm env "int8" " " "llvmptr 8" "exists x:bv 8.eq(llvmword(x))";
heapster_define_perm env "int1" " " "llvmptr 1" "exists x:bv 1.eq(llvmword(x))";

heapster_define_llvmshape env "u64" 64 "" "fieldsh(int64<>)";
heapster_define_llvmshape env "u32" 64 "" "fieldsh(32,int32<>)";
heapster_define_llvmshape env "u8" 64 "" "fieldsh(8,int8<>)";

heapster_define_llvmshape env "usize" 64 "" "fieldsh(int64<>)";
heapster_define_llvmshape env "char" 64 "" "fieldsh(32,int32<>)";

// bool type
heapster_define_llvmshape env "bool" 64 "" "fieldsh(1,int1<>)";

// Box type
heapster_define_llvmshape env "Box" 64 "T:llvmshape 64" "ptrsh(T)";

// Infallible type
// Note that this type is actually uninhabited
heapster_define_llvmshape env "Infallible" 64 "" "falsesh";

// ControlFlow type
heapster_define_rust_type env
  "pub enum ControlFlow<B, C> { Continue (C), Break (B) }";

// The Option type
heapster_define_rust_type env "pub enum Option<X> { None, Some (X) }";

// Result type
heapster_define_rust_type env "pub enum Result<X,Y> { Ok (X), Err (Y) }";

// Special case for types of the form Result<bool,T>
heapster_define_llvmshape env "Resultbool" 64 "T:llvmshape 64"
  "(fieldsh(8,eq(llvmword(0)));fieldsh(8,int8<>);fieldsh(48,true);emptysh) orsh \
 \ (fieldsh(8,eq(llvmword(1)));fieldsh(56,true);T)";

// The String type
heapster_define_llvmshape env "String" 64 ""
  "exsh cap:bv 64. ptrsh(arraysh(<cap,*1,fieldsh(8,int8<>))); \
                 \ fieldsh(int64<>);fieldsh(eq(llvmword(cap)))";

// Opaque type for Vec<T>
heapster_define_opaque_llvmshape env "Vec" 64
  "T:llvmshape 64" "24"
  "\\ (T:sort 0) -> List T";

// Iter type: we define this as a sequence of two LLVM values, which have opaque
// permissions; this allows Iter to be laid out as a pair of LLVM values while
// still having an opaque "view" of those values
heapster_define_opaque_perm env "Iter_fld1" "T:llvmshape 64"
  "llvmptr 64" "\\ (T:sort 0) -> List T";
heapster_define_opaque_perm env "Iter_fld2" "" "llvmptr 64" "#()";
heapster_define_llvmshape env "Iter" 64 "l:lifetime, T:llvmshape 64"
  "fieldsh(Iter_fld1<T>);fieldsh(Iter_fld2<>)";

// Rev type: A simple wrapper around a type, so we define it for real
heapster_define_rust_type env "pub struct Rev<T> { iter: T, }";

// Var type
heapster_define_rust_type env "pub struct Var(String);";

// FunName type
heapster_define_rust_type env "pub struct FunName(String);";

// PrimOp2 type
heapster_define_rust_type env
  "pub enum PrimOp2 { AddOp, MulOp }";

// Lit type
heapster_define_rust_type env
  "pub enum Lit { \
    \ BoolLit (bool), \
    \ IntLit (u64), }";

heapster_define_rust_type env
  "pub enum Expr { \
    \ LitExpr(Lit), \
    \ VarExpr(Var), \
    \ Apply2Expr(PrimOp2, Box<Expr>, Box<Expr>) }";

heapster_define_rust_type env
  "pub enum Stmt { \
    \ Return(Var), \
    \ Skip, \
    \ Assign(Var,Expr), \
    \ IfStmt(Expr,Box<Stmt>,Box<Stmt>), \
    \ WhileStmt(Expr,Box<Stmt>), \
    \ CallStmt(FunName, Vec<Expr>) }";

heapster_define_rust_type env
  "pub enum Value { \
   \ BoolValue (bool), \
   \ IntValue (u64), \
   \ StringValue (String) }";

heapster_define_rust_type env
  "pub enum Error { \
   \ UnboundVar(Var), \
   \ UnknownFun(FunName), \
   \ Unimplemented, \
   \ IncorrectType, \
   \ WrongNumberOfArgs, }";

heapster_define_rust_type env
  "pub struct FunDef (Vec<Var>,Stmt);";

heapster_define_rust_type env
  "pub struct Env (Vec<(Var, Value)>, \
                 \ Vec<(FunName, FunDef)>);";


/***
 *** Assumed Functions
 ***/

// llvm.uadd.with.overflow.i64
heapster_assume_fun env "llvm.uadd.with.overflow.i64"
  "(). arg0:int64<>, arg1:int64<> -o ret:struct(int64<>,int1<>)"
  "\\ (x y:Vec 64 Bool) -> \
    \ returnM (Vec 64 Bool * Vec 1 Bool) (bvAdd 64 x y, single Bool (bvCarry 64 x y))";

// llvm.expect.i1
heapster_assume_fun env "llvm.expect.i1"
  "().arg0:int1<>, arg1:int1<> -o ret:int1<>" "\\ (x y:Vec 1 Bool) -> returnM (Vec 1 Bool) x";


// memcpy
heapster_assume_fun env "llvm.memcpy.p0i8.p0i8.i64"
  "(rw:rwmodality, l1:lifetime, l2:lifetime, sh:llvmshape 64, \
   \ b:llvmblock 64, len:bv 64). \
  \ arg0:[l1]memblock(W,0,len,sh), arg1:[l2]memblock(rw,0,len,eqsh(len,b)), \
  \ arg2:eq(llvmword(len)) -o \
  \ arg0:[l1]memblock(W,0,len,eqsh(len,b)), arg1:[l2]memblock(rw,0,len,eqsh(len,b))"
  "\\ (X:sort 0) (len:Vec 64 Bool) (x:X) (_:#()) -> returnM (#() * #()) ((),())";

// Var::clone
var_clone_sym <- heapster_find_symbol env
  "$LT$interp..Var$u20$as$u20$core..clone..Clone$GT$5clone";
heapster_assume_fun_rename_prim env var_clone_sym "var_clone"
  "<'a> fn (x:&'a Var) -> Var";

// Vec<(Var, Value)>::deref
vec_var_deref_sym <- heapster_find_symbol_with_type env
  "core..ops..deref..Deref$GT$5deref"
  "{ [0 x { %Var, %Value }]*, i64 }(%\"alloc::vec::Vec<(Var, Value)>\"*)";
heapster_assume_fun_rename_prim env vec_var_deref_sym "vec_var_deref"
  "<'a> fn (&'a Vec<(Var, Value)>) -> &'a[(Var, Value)]";

// &[(Var, Value)]::iter
slice_var_iter_sym <- heapster_find_symbol_with_type env
  "core5slice29_$LT$impl$u20$$u5b$T$u5d$$GT$4iter"
  "{ i64*, i64* }([0 x { %Var, %Value }]*, i64)";
heapster_assume_fun_rename_prim env slice_var_iter_sym "slice_var_iter"
  "<'a> fn (&'a[(Var, Value)]) -> Iter<'a, (Var, Value)>";

// NOTE: Unfortunately there are two symbols with the same type for rev,
// so we need to just use the full hash for searching
// Iter<'a, (Var, Value)>::rev
iter_var_rev_sym <- heapster_find_symbol env
  "core4iter6traits8iterator8Iterator3rev17hd35750bd4d210bc7E";
heapster_assume_fun_rename_prim env iter_var_rev_sym "iter_var_rev"
  "<'a> fn (Iter<'a, (Var, Value)>) -> Rev<Iter<'a, (Var, Value)>>";

// Vec<(Var, Value)>::new
vec_new_sym <- heapster_find_symbol env "Vec$LT$T$GT$3new";
heapster_assume_fun_rename_prim env vec_new_sym "vec_new"
  "<> fn () -> Vec<(Var, Value)>";

// Vec<(Var, Value)>::push
vec_push_sym <- heapster_find_symbol env "4push";
heapster_assume_fun_rename_prim env vec_push_sym "vec_push"
  "<'a> fn(&'a mut Vec<(Var, Value)>, (Var, Value))";

// Vec<(FunName, FunDef)>::clone
vec_clone_sym <- heapster_find_symbol_with_type env
  "core..clone..Clone$GT$5clone"
  "void(%\"alloc::vec::Vec<(FunName, FunDef)>\"*, %\"alloc::vec::Vec<(FunName, FunDef)>\"*)";
heapster_assume_fun_rename_prim env vec_clone_sym "vec_clone"
  "<'a> fn (&'a Vec<(FunName, FunDef)>) -> Vec<(FunName, FunDef)>";

// Vec<(FunName, FunDef)>::deref
vec_fun_deref_sym <- heapster_find_symbol_with_type env
  "core..ops..deref..Deref$GT$5deref"
  "{ [0 x { %FunName, %FunDef }]*, i64 }(%\"alloc::vec::Vec<(FunName, FunDef)>\"*)";
heapster_assume_fun_rename_prim env vec_fun_deref_sym "vec_fun_deref"
  "<'a> fn (&'a Vec<(FunName, FunDef)>) -> &'a[(FunName, FunDef)]";

// &[(FunName, FunDef)]::iter
slice_fun_iter_sym <- heapster_find_symbol_with_type env
  "core5slice29_$LT$impl$u20$$u5b$T$u5d$$GT$4iter"
  "{ i64*, i64* }([0 x { %FunName, %FunDef }]*, i64)";
heapster_assume_fun_rename_prim env slice_fun_iter_sym "slice_fun_iter"
  "<'a> fn (&'a[(FunName, FunDef)]) -> Iter<'a, (FunName, FunDef)>";

// NOTE: Unfortunately there are two symbols with the same type for rev,
// so we need to just use the full hash for searching
// Iter<'a, (FunName, FunDef)>::rev
iter_fun_rev_sym <- heapster_find_symbol env
  "core4iter6traits8iterator8Iterator3rev17h221acc9c965305cbE";
heapster_assume_fun_rename_prim env iter_fun_rev_sym "iter_fun_rev"
  "<'a> fn (Iter<'a, (Var, Value)>) -> Rev<Iter<'a, (Var, Value)>>";

// Value::drop_in_place
Value__drop_in_place_sym <- heapster_find_symbol_with_type env
  "core3ptr34drop_in_place$LT$interp..Value$"
  "void(%Value*)";
heapster_assume_fun_rename_prim env Value__drop_in_place_sym
  "Value__drop_in_place"
  "<> fn(Value)";

// TODO: Are these lifetimes right?
// &Var::eq
var_eq_sym <- heapster_find_symbol_with_type env
  "PartialEq$LT$$RF$B$GT$$u20$for$u20$$RF$A$GT$2eq"
  "i1(%Var**, %Var**)";
heapster_assume_fun_rename_prim env var_eq_sym "var_eq"
  "<'a, 'b, 'c> fn (&'b&'a Var, &'c&'a Var) -> bool";

// TODO: Are these lifetimes right?
// &FunName::eq
fun_eq_sym <- heapster_find_symbol_with_type env
  "PartialEq$LT$$RF$B$GT$$u20$for$u20$$RF$A$GT$2eq"
  "i1(%FunName**, %FunName**)";
heapster_assume_fun_rename_prim env fun_eq_sym "fun_eq"
  "<'a, 'b, 'c> fn (&'b&'a FunName, &'c&'a FunName) -> bool";

// Result<u64, Error>::branch
u64_branch_sym <- heapster_find_symbol_with_type env
  "core..ops..try_trait..Try$GT$6branch"
  "void(%\"core::ops::control_flow::ControlFlow<core::result::Result<core::convert::Infallible, Error>, u64>\"*, %\"core::result::Result<u64, Error>\"*)";
heapster_assume_fun_rename_prim env u64_branch_sym "u64_branch"
  "<> fn (Result<u64, Error>) -> ControlFlow<Result<Infallible, Error>, u64>";

// Result<Value, Error>::branch
value_branch_sym <- heapster_find_symbol_with_type env
  "core..ops..try_trait..Try$GT$6branch"
  "void(%\"core::ops::control_flow::ControlFlow<core::result::Result<core::convert::Infallible, Error>, Value>\"*, %\"core::result::Result<Value, Error>\"*)";
heapster_assume_fun_rename_prim env value_branch_sym "value_branch"
  "<> fn (Result<Value, Error>) -> ControlFlow<Result<Infallible, Error>, Value>";

// Result<&FunDef, Error>::branch
fun_branch_sym <- heapster_find_symbol_with_type env
  "core..ops..try_trait..Try$GT$6branch"
  "void(%\"core::ops::control_flow::ControlFlow<core::result::Result<core::convert::Infallible, Error>, &FunDef>\"*, %\"core::result::Result<&FunDef, Error>\"*)";
heapster_assume_fun_rename_prim env fun_branch_sym "fun_branch"
  "<'a> fn (Result<&'a FunDef, Error>) -> ControlFlow<Result<Infallible, Error>, &'a FunDef>";

/***
 *** Type-checked Functions
 ***/

fresh_env_from_env_sym <- heapster_find_symbol env "18fresh_env_from_env";
heapster_typecheck_fun_rename env fresh_env_from_env_sym "fresh_env_from_env"
  "<'a> fn (&'a Env) -> Env";

// TODO: Investigate the error this one produces when actually checked
value_bool_sym <- heapster_find_symbol env "10value_bool";
heapster_typecheck_fun_rename env value_bool_sym "value_bool"
  "<> fn (Value) -> Resultbool<Error>";

value_u64_sym <- heapster_find_symbol env "9value_u64";
heapster_typecheck_fun_rename env value_u64_sym "value_u64"
  "<> fn (Value) -> Result<u64, Error>";

// TODO: Investigate the error this one produces when actually checked
lit_value_sym <- heapster_find_symbol env "9lit_value";
heapster_typecheck_fun_rename env lit_value_sym "lit_value"
  "<'a> fn(&'a Lit) -> Value";

// TODO: See above note on the `eq` implementation
ref_ref_eq_var_sym <- heapster_find_symbol_with_type env
  "10ref_ref_eq"
  "i1(%Var*, %Var**)";
heapster_typecheck_fun_rename env ref_ref_eq_var_sym "ref_ref_eq_var"
  "<'a, 'b, 'c> fn (&'a Var, &'b&'c Var) -> bool";

// TODO: See above note on the `eq` implementation
ref_ref_eq_fun_sym <- heapster_find_symbol_with_type env
  "10ref_ref_eq"
  "i1(%FunName*, %FunName**)";
heapster_typecheck_fun_rename env ref_ref_eq_fun_sym "ref_ref_eq_fun"
  "<'a, 'b, 'c> fn (&'a FunName, &'b&'c FunName) -> bool";

env_lookup_sym <- heapster_find_symbol env "10env_lookup17";
heapster_typecheck_fun_rename env env_lookup_sym "env_lookup"
  "<'a,'b> fn (env : &'a Env, x : &'b Var) -> Result<Value,Error>";

env_update_sym <- heapster_find_symbol env "10env_update";
heapster_typecheck_fun_rename env env_update_sym "env_update"
  "<'a,'b> fn (&'a mut Env, &'b Var, Value)";

fun_lookup_sym <- heapster_find_symbol env "10fun_lookup17";
heapster_typecheck_fun_rename env fun_lookup_sym "fun_lookup"
  "<'a, 'b> fn (&'a Env, &'b FunName) -> Result<&'a FunDef, Error>";

// TODO: Add stuff related to the `?` operator
eval_primop2_sym <- heapster_find_symbol env "12eval_primop2";
heapster_typecheck_fun_rename env eval_primop2_sym "eval_primop2"
  "<'a> fn (&'a PrimOp2, Value, Value) -> Result<Value, Error>";

// TODO: Add stuff related to the `?` operator
eval_expr_sym <- heapster_find_symbol env "9eval_expr";
heapster_typecheck_fun_rename env eval_expr_sym "eval_expr"
  "<'a, 'b> fn(&'a Env, &'b Expr) -> Result<Value, Error>";

eval_stmt_sym <- heapster_find_symbol env "9eval_stmt";
heapster_typecheck_fun_rename env eval_stmt_sym "eval_stmt"
  "<'a, 'b> fn (&'a mut Env, &'b Stmt) -> Result<Value, Error>";

/***
 *** Export to Coq
 ***/

heapster_export_coq env "interp_gen.v";
